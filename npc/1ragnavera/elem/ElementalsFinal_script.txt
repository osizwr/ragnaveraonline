// ==== *Athena Script =================================
// Kamishi's Palettes Protection V.1.0
// Never show this file to anyone, except yourself. ;)
// =====================================================

// =====================================================
// Initialize the hash table required to access secret 
// palette file numbers.
// =====================================================

prontera,179,27,5	script	#121345	111,{
	end;
	OnPcLoginEvent:
		if (!setPaletteToProtectedNumber == 2){
			callfunc("setlook2", 6, realPlayerLookHair);
			callfunc("setlook2", 7, realPlayerLookClothes);
			set setPaletteToProtectedNumber,2;
		}
		end;

	OnInit:
		set $paletteSECRET1, 179;
		set $paletteMAGIC2, 17;
		set $paletteSECRET3, 7000;
		set $paletteOFFSET, 600;
		setarray $paletteHash[0],20,89,17,88,171,24,58,98,68,55,110,145,127,117,73,27,175,75,155,46,9,10,134,71,137,157,60,76,62,34,86,31,106,64,74,147,164,150,109,130,8,151,49,61,179,30,78,119,26,96,139,115,37,168,2,172,80,48,111,103,160,122,67,84,50,142,94,70,22,159,101,95,125,44,167,112,152,158,140,79,149,18,83,169,33,43,146,52,173,11,131,87,113,135,57,138,54,129,40,97,91,144,82,63,126,165,53,90,32,21,66,15,116,51,59,77,72,136,93,45,29,121,5,118,13,128,100;
		end;
}


// ====== Access the secret file palette number. =======
// Method replacement for setlook, changelook, getlook.
// ==> setLook2, getLook2, changeLook2
// =====================================================


function	script	getProtectedPal	{
	set @i, getarg(0);
	
	if(@i == 0) return 0;

	set @type, getarg(1);
	if(@type == 7){
		if(@i < 5) return @i; // set 5 to your unprotected clothes palette #.
	}
	if(@type == 6){
		if(@i < 9) return @i; // set 9 to your unprotected hair palette #.
	}

	set @m, @i / 127; 
	set @d, $paletteHash[(@i +($paletteMAGIC2*@m)) % 127] + ($paletteSECRET1* @m);
	set @d, $paletteSECRET3-@d;
	return @d+$paletteOFFSET;
}

function	script	setlook2	{
	//Save current value:
	set @type, getarg(0);
	set @i, getarg(1);
	
	
	if(@type == 6){
		set realPlayerLookHair, @i;		
	
	}else if (@type == 7){
		set realPlayerLookClothes, @i;		
	}else{
		setlook @type, @i;
		return;
	}
	
	setlook @type, callfunc("getProtectedPal", @i, @type);
	
	return;
}

function	script	changelook2	{
	//Save current value:
	set @type, getarg(0);
	set @i, getarg(1);
	
	
	if((@type == 6) || (@type == 7)){
		changelook @type, callfunc("getProtectedPal", @i, @type);
	}else{
		changelook @type, @i;
	}
	
	
	return;
}

function	script	getlook2	{
	set @type, getarg(0);
	
	if(@type == 6){
		return realPlayerLookHair;		
	}else if (@type == 7){
		return realPlayerLookClothes;		
	}else{
		return getlook(@type);
	}
}

		